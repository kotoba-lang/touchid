(ns touchid.core-test
  (:require [clojure.test :refer [deftest is]]
            [touchid.core :as c]
            [touchid.datom :as d]
            [touchid.model :as m]
            [touchid.ports :as p]))

(deftest accepts-attestation-metadata
  (let [req (m/request "t1" {:subject "did:web:example.com:alice"})
        port (reify p/ITouchID
               (attest! [_ r] (m/attestation r true {:device-id "macbook"})))]
    (is (true? (:touchid/ok? (c/attest port req))))))

(deftest emits-attestation-datoms
  (let [req (m/request "t1" {:subject "did:web:example.com:alice"})
        att (m/attestation req true {:device-id "macbook"})]
    (is (= [{:db/id "t1"
             :touchid/ok? true
             :touchid/purpose :step-up
             :touchid/subject "did:web:example.com:alice"
             :touchid/device-id "macbook"
             :touchid/credential-id nil
             :touchid/provider nil
             :touchid/evidence-ref nil
             :touchid/attested-at nil}]
           (d/attestation-datoms att)))))

(deftest rejects-fingerprint-template
  (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
               (c/attest (p/missing) (assoc (m/request "t2" {}) :fingerprint-template "no")))))

(deftest consumes-challenge-once-and-stores-device-bound-attestation
  (let [req (m/request "t3" {:subject "did:web:example.com:alice"
                             :challenge "challenge-1"})
        challenge-store (p/memory-challenge-store)
        attestation-store (p/memory-attestation-store)
        port (reify p/ITouchID
               (attest! [_ r] (m/attestation r true {:device-id "macbook"
                                                     :credential-id "cred-1"})))
        out (c/attest-once-and-store! challenge-store attestation-store port req)]
    (is (= "macbook" (:touchid/device-id out)))
    (is (= [out] (p/attestations-for-device attestation-store "macbook")))
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/attest-once-and-store! challenge-store attestation-store port req)))))

(deftest rejects-storage-without-device-binding
  (let [req (m/request "t4" {:subject "did:web:example.com:alice"})
        att (m/attestation req true {})]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/attest-and-store! (p/memory-attestation-store) att)))))
